plugins {
    id 'org.beryx.runtime' version '1.8.4'
}
apply plugin: "java"

sourceCompatibility = 11
sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs += [ project.file('assets').path ]

mainClassName = "com.ray3k.skincomposer.uninstaller.Uninstaller"
project.ext.assetsDir = new File("/assets")

def exeName = 'SkinComposerUninstaller'
def mtPath = 'C:/Program Files (x86)/Windows Kits/10/bin/10.0.19041.0/x64/mt.exe'

task dist(type: Jar) {
    archiveFileName = "${project.appName}-uninstaller.jar";
    from files(sourceSets.main.output.getClassesDirs())
    from files(sourceSets.main.output.resourcesDir)
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    dependsOn configurations.runtimeClasspath
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    from files(project.assetsDir)
    destinationDirectory = file("$buildDir/lib")
    manifest {
        attributes ('Main-Class': 'com.ray3k.skincomposer.uninstaller.Uninstaller')
    }
}

dist.dependsOn classes
jpackageImage.dependsOn dist

// changes file permission of JavaPackager executable to read and write
task filepermission(type: Exec) {
    commandLine 'attrib', '-R', "${buildDir}/jpackage/${exeName}/${exeName}.exe"
}

// Use mt.exe to apply a manifest to the JavaPackager executable
task jpackageApplyUAC(type: Exec, dependsOn: [jpackageImage, filepermission]) {
    tasks.findByName('filepermission').mustRunAfter jpackageImage

    workingDir project.projectDir
    commandLine = [
            mtPath,
            '-manifest', "${project.projectDir}/SkinComposerUninstaller.exe.manifest",
            "-outputresource:${buildDir}\\jpackage\\${exeName}\\${exeName}.exe;#1"
    ]
}

runtime {
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages',
               '--strip-native-commands',
               '--vm', 'server']
    modules = ['java.base' ,
               'java.desktop',
               'jdk.unsupported',
               'java.compiler']
    distDir = file("$buildDir")
    jpackage {
        mainJar = "${appName}-uninstaller.jar"
        imageOptions = ['--icon', file('../icons/logo.ico')]
        imageName = exeName
    }
}